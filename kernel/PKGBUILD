pkgver=6.18.0
nv=580.105.08

pkgname=linux-zenixark
depends=(initramfs nvidia-utils=$nv)
makedepends=(bc clang lld llvm perl python)
arch=(x86_64) options=(!strip) pkgrel=1

source=(config

        https://cdn.kernel.org/pub/linux/kernel/v${pkgver%%.*}.x/linux-${pkgver%.0}.tar.xz
        https://us.download.nvidia.com/XFree86/Linux-x86_64/$nv/NVIDIA-Linux-x86_64-$nv.run

        https://raw.githubusercontent.com/CachyOS/kernel-patches/refs/heads/master/${pkgver%.*}/all/0001-cachyos-base-all.patch
        https://raw.githubusercontent.com/CachyOS/kernel-patches/refs/heads/master/${pkgver%.*}/sched/0001-bore-cachy.patch
        https://raw.githubusercontent.com/CachyOS/kernel-patches/refs/heads/master/${pkgver%.*}/misc/nvidia/0001-Enable-atomic-kernel-modesetting-by-default.patch

        git+https://gitlab.com/kernel-firmware/linux-firmware.git
        git+https://github.com/intel/Intel-Linux-Processor-Microcode-Data-Files)

sha256sums=('169e2f3aa64d335d81816d8fca5c0fe67570e250b71559e051053857cf4120ec'
            '9106a4605da9e31ff17659d958782b815f9591ab308d03b0ee21aad6c7dced4b'
            'd9c6e8188672f3eb74dd04cfa69dd58479fa1d0162c8c28c8d17625763293475'
            '3499c3b7c4b91caffcec227acc42584e05fb3955f8e29c3434f5f3ad17aa63c2'
            '3d07c4d97de4cbc402d82c9e60998149b84aeabe339e87e2dc1636c89a28a57f'
            '163c57160cc1033020680f638b44ebd94b496152dcd8951e87d5077d4d5c2009'
            'SKIP'
            'SKIP')

prepare() {
    sh NVIDIA-Linux-x86_64-$nv.run --extract-only
    mv linux-firmware/intel/iwlwifi/* Intel-Linux-Processor-Microcode-Data-Files/intel-ucode linux-firmware

    cd linux-${pkgver%.0}

    echo -$pkgrel > localversion.10-pkgrel
    echo ${pkgname#linux} > localversion.20-pkgname

    for src in "${source[@]}"; do
        [[ $src = *.patch* ]] && patch -Np1 $([[ $src = *nvidia* ]] && echo "-d $srcdir/NVIDIA-Linux-x86_64-$nv/kernel") < ../${src##*/}
    done

    cp ../config .config
    make CC=clang LD=ld.lld LLVM=1 LLVM_IAS=1 olddefconfig
    diff --color=always ../config .config || read -p "Press enter to confirm"

    make -s kernelrelease > version
}

build() {
    cd linux-${pkgver%.0}
    make CC=clang LD=ld.lld LLVM=1 LLVM_IAS=1 -j$(nproc) all

    cd $srcdir/NVIDIA-Linux-x86_64-$nv/kernel
    make CC=clang LD=ld.lld LLVM=1 LLVM_IAS=1 KERNEL_UNAME=$pkgver-${pkgname#linux-} SYSSRC=$srcdir/linux-${pkgver%.0} SYSOUT=$srcdir/linux-${pkgver%.0} NV_EXCLUDE_BUILD_MODULES=__EXCLUDE_MODULES IGNORE_CC_MISMATCH=yes -j$(nproc) modules
}

package() {
    cd linux-${pkgver%.0}
    modulesdir=$pkgdir/usr/lib/modules/$(<version)

    install -Dm644 "$(make -s image_name)" $modulesdir/vmlinuz
    install -Dm644 <(echo $pkgname) $modulesdir/pkgbase

    install -Dm644 $srcdir/NVIDIA-Linux-x86_64-$nv/kernel/*.ko $modulesdir
    find $modulesdir -name '*.ko' -print -exec llvm-strip --strip-debug {} \; -exec scripts/sign-file sha512 certs/signing_key.pem certs/signing_key.x509 {} \; -exec zstd --rm -19 -T0 {} +

    ZSTD_CLEVEL=19 make CC=clang LD=ld.lld LLVM=1 LLVM_IAS=1 INSTALL_MOD_PATH=$pkgdir/usr INSTALL_MOD_STRIP=1 DEPMOD=/doesnt/exist modules_install
    rm $modulesdir/build
}
