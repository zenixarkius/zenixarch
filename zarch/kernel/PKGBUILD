# Maintainer: zenixark <contact@zenixark.com>

pkgname=linux-zenixark
pkgbase=$pkgname
pkgver=6.17.5
pkgrel=1
pkgdesc="Zenixark"
url="https://zenixark.com/zenixark/zenixarch"
arch=(x86_64)
makedepends=(bc llvm pahole python rust-bindgen rust-src)
options=(!debug !strip !lto)

_srcname=linux-${pkgver}
_nv_ver=580.95.05
source=(https://cdn.kernel.org/pub/linux/kernel/v${pkgver%%.*}.x/${_srcname}.tar.xz
        https://raw.githubusercontent.com/CachyOS/kernel-patches/refs/heads/master/${pkgver%.*}/all/0001-cachyos-base-all.patch
        https://raw.githubusercontent.com/CachyOS/kernel-patches/refs/heads/master/${pkgver%.*}/sched/0001-bore-cachy.patch
        https://us.download.nvidia.com/XFree86/Linux-x86_64/${_nv_ver}/NVIDIA-Linux-x86_64-${_nv_ver}.run
        config)

export KBUILD_BUILD_HOST=zenixark
export KBUILD_BUILD_USER=$pkgbase
export KBUILD_BUILD_TIMESTAMP="$(date -Ru${SOURCE_DATE_EPOCH:+d @$SOURCE_DATE_EPOCH})"

FLAGS=(CC=clang LD=ld.lld LLVM=1 LLVM_IAS=1)

prepare() {
    cd $_srcname

    echo "Setting version..."
    echo "-$pkgrel" > localversion.10-pkgrel
    echo "${pkgbase#linux}" > localversion.20-pkgname

    local src
    for src in "${source[@]}"; do
        src="${src%%::*}"
        src="${src##*/}"
        src="${src%.zst}"
        [[ $src = *.patch ]] || continue
        echo "Applying patch $src..."
        patch -Np1 < "../$src"
    done

    echo "Setting config..."
    cp ../config .config
    make "${FLAGS[@]}" olddefconfig
    diff -u ../config .config || :

    make -s kernelrelease > version
    echo "Prepared $pkgbase version $(<version)"

    echo "Preparing NVIDIA modules in tree..."
    (cd $srcdir && sh NVIDIA-Linux-x86_64-"${_nv_ver}".run --extract-only)
    mkdir -p drivers/nvidia
    cp -r "${srcdir}/NVIDIA-Linux-x86_64-${_nv_ver}"/kernel/* drivers/nvidia/
    echo "obj-y += nvidia/" >> drivers/Makefile
}

build() {
    cd $_srcname
    make "${FLAGS[@]}" -j"$(nproc)" all
}

package() {
    pkgdesc="The $pkgdesc kernel"
    depends=(coreutils initramfs nvidia-utils=${_nv_ver} libglvnd)
    optdepends=('linux-firmware: firmware images needed for some devices')
    provides=(NTSYNC-MODULE NVIDIA-MODULE WIREGUARD-MODULE)

    cd $_srcname

    local modulesdir="$pkgdir/usr/lib/modules/$(<version)"

    echo "Installing boot image..."
    # systemd expects to find the kernel here to allow hibernation
    # https://github.com/systemd/systemd/commit/edda44605f06a41fb86b7ab8128dcf99161d2344
    install -Dm644 "$(make "${FLAGS[@]}" -s image_name)" "$modulesdir/vmlinuz"

    # Used by mkinitcpio to name the kernel
    echo "$pkgbase" | install -Dm644 /dev/stdin "$modulesdir/pkgbase"
}
