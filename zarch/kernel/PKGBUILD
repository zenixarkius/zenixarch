# Maintainer: zenixark <contact@zenixark.com>

pkgbase=linux-zenixark
pkgver=6.17.5
pkgrel=1
pkgdesc="Zenixark"
url="https://zenixark.com/zenixark/zenixarch"
arch=(x86_64)
makedepends=(bc clang cpio gettext libelf lld llvm pahole perl python rust rust-bindgen rust-src tar xz)
options=(!debug !strip !lto)

_srcname=linux-${pkgver}
_nv_ver=580.95.05
source=(https://cdn.kernel.org/pub/linux/kernel/v${pkgver%%.*}.x/${_srcname}.tar.xz
        https://raw.githubusercontent.com/CachyOS/kernel-patches/refs/heads/master/${pkgver%.*}/all/0001-cachyos-base-all.patch
        https://raw.githubusercontent.com/CachyOS/kernel-patches/refs/heads/master/${pkgver%.*}/sched/0001-bore-cachy.patch
        https://us.download.nvidia.com/XFree86/Linux-x86_64/${_nv_ver}/NVIDIA-Linux-x86_64-${_nv_ver}.run
        config)

export KBUILD_BUILD_HOST=zenixark
export KBUILD_BUILD_USER=$pkgbase
export KBUILD_BUILD_TIMESTAMP="$(date -Ru${SOURCE_DATE_EPOCH:+d @$SOURCE_DATE_EPOCH})"

prepare() {
    cd $_srcname

    echo "Setting version..."
    echo "-$pkgrel" > localversion.10-pkgrel
    echo "${pkgbase#linux}" > localversion.20-pkgname

    local src
    for src in "${source[@]}"; do
        src="${src%%::*}"
        src="${src##*/}"
        src="${src%.zst}"
        [[ $src = *.patch ]] || continue
        echo "Applying patch $src..."
        patch -Np1 < "../$src"
    done

    echo "Setting config..."
    cp ../config .config
    make olddefconfig
    diff -u ../config .config || :

    make -s kernelrelease > version
    echo "Prepared $pkgbase version $(<version)"

    cd $srcdir
    sh NVIDIA-Linux-x86_64-"${_nv_ver}".run --extract-only
    cd $_srcname
    mkdir -p drivers/nvidia
    cp -r "${srcdir}/NVIDIA-Linux-x86_64-${_nv_ver}"/kernel/* drivers/nvidia/
    echo "obj-y += nvidia/" >> drivers/Makefile
}

build() {
    cd $_srcname
    make CC=clang LD=ld.lld LLVM=1 LLVM_IAS=1 -j"$(nproc)" all
    make -C tools/bpf/bpftool vmlinux.h feature-clang-bpf-co-re=1
}

package() {
    pkgdesc="The $pkgdesc kernel and modules"
    depends=(coreutils kmod initramfs nvidia-utils=${_nv_ver} libglvnd)
    optdepends=('linux-firmware: firmware images needed for some devices'
                'scx-scheds: to use sched-ext schedulers'
                'wireless-regdb: to set the correct wireless channels of your country')
    provides=(KSMBD-MODULE NTSYNC-MODULE NVIDIA-MODULE VIRTUALBOX-GUEST-MODULES WIREGUARD-MODULE)

    cd $_srcname

    local modulesdir="$pkgdir/usr/lib/modules/$(<version)"

    echo "Installing boot image..."
    # systemd expects to find the kernel here to allow hibernation
    # https://github.com/systemd/systemd/commit/edda44605f06a41fb86b7ab8128dcf99161d2344
    install -Dm644 "$(make -s image_name)" "$modulesdir/vmlinuz"

    # Used by mkinitcpio to name the kernel
    echo "$pkgbase" | install -Dm644 /dev/stdin "$modulesdir/pkgbase"

    echo "Installing modules..."
    ZSTD_CLEVEL=19 make CC=clang LD=ld.lld LLVM=1 LLVM_IAS=1 INSTALL_MOD_PATH="$pkgdir/usr" INSTALL_MOD_STRIP=1 \
        DEPMOD=/doesnt/exist modules_install  # Suppress depmod

    # remove build link
    rm "$modulesdir"/build
}
